# We have a conf and classes directory, add to BBPATH
BBPATH .= ":${LAYERDIR}"

# We have a packages directory, add to BBFILES
BBFILES += "${LAYERDIR}/recipes-*/*/*.bb"
BBFILES += "${LAYERDIR}/recipes-*/*/*.bbappend"

BBFILE_COLLECTIONS += "xilinx"
BBFILE_PATTERN_xilinx = "^${LAYERDIR}/"
BBFILE_PRIORITY_xilinx = "5"

BBFILES_DYNAMIC += " \
openembedded-layer:${LAYERDIR}/dynamic-layers/openembedded-layer/recipes-*/*/*.bb \
openembedded-layer:${LAYERDIR}/dynamic-layers/openembedded-layer/recipes-*/*/*.bbappend \
chromium-browser-layer:${LAYERDIR}/dynamic-layers/chromium-browser-layer/recipes-*/*/*.bb \
chromium-browser-layer:${LAYERDIR}/dynamic-layers/chromium-browser-layer/recipes-*/*/*.bbappend \
meta-python:${LAYERDIR}/dynamic-layers/meta-python/recipes-*/*/*.bb \
meta-python:${LAYERDIR}/dynamic-layers/meta-python/recipes-*/*/*.bbappend \
virtualization-layer:${LAYERDIR}/dynamic-layers/virtualization-layer/recipes-*/*/*.bb \
virtualization-layer:${LAYERDIR}/dynamic-layers/virtualization-layer/recipes-*/*/*.bbappend \
xilinx-tools:${LAYERDIR}/dynamic-layers/meta-xilinx-tools/recipes-*/*/*.bb \
xilinx-tools:${LAYERDIR}/dynamic-layers/meta-xilinx-tools/recipes-*/*/*.bbappend \
"

LAYERDEPENDS_xilinx = "core"
LAYERRECOMMENDS_xilinx = "openembedded-layer chromium-browser-layer"

LAYERSERIES_COMPAT_xilinx = "langdale"

SIGGEN_EXCLUDE_SAFE_RECIPE_DEPS += " \
  *->zocl \
  *->cairo \
  *->libepoxy \
  *->gstreamer1.0-plugins-base \
  *->gtk+ \
  *->gtk+3 \
  *->libglu \
  *->libsdl \
  *->libsdl2 \
  *->mesa \
  *->mesa-gl \
  *->qemu \
  *->virglrenderer \
  *->xserver-xorg \
"

XILINX_RELEASE_VERSION ??= "v2022.2"

BUILDCFG_VARS:append = " XILINX_RELEASE_VERSION"

XILINX_QEMU_VERSION[v2022.1] = "v6.1.0-xilinx-v2022.1%"
XILINX_QEMU_VERSION[v2022.2] = "v6.1.0-xilinx-v2022.2%"
PREFERRED_VERSION_qemu-xilinx ?= "${@d.getVarFlag('XILINX_QEMU_VERSION', d.getVar('XILINX_RELEASE_VERSION')) or 'undefined'}"
PREFERRED_VERSION_qemu-xilinx-native ?= "${@d.getVarFlag('XILINX_QEMU_VERSION', d.getVar('XILINX_RELEASE_VERSION')) or 'undefined'}"
PREFERRED_VERSION_qemu-xilinx-system-native ?= "${@d.getVarFlag('XILINX_QEMU_VERSION', d.getVar('XILINX_RELEASE_VERSION')) or 'undefined'}"
PREFERRED_VERSION_qemu-devicetrees ?= "xilinx-${XILINX_RELEASE_VERSION}%"

XILINX_ATF_VERSION[v2022.1] = "2.6-xilinx-v2022.1%"
XILINX_ATF_VERSION[v2022.2] = "2.6-xilinx-v2022.2%"
PREFERRED_VERSION_arm-trusted-firmware ?= "${@d.getVarFlag('XILINX_ATF_VERSION', d.getVar('XILINX_RELEASE_VERSION')) or 'undefined'}"

XILINX_UBOOT_VERSION[v2022.1] = "v2021.01-xilinx-v2022.1%"
XILINX_UBOOT_VERSION[v2022.2] = "v2021.01-xilinx-v2022.2%"

PREFERRED_VERSION_u-boot-xlnx ?= "${@d.getVarFlag('XILINX_UBOOT_VERSION', d.getVar('XILINX_RELEASE_VERSION')) or 'undefined'}"

XILINX_LINUX_VERSION[v2022.1] = "5.15.19-xilinx-v2022.1%"
XILINX_LINUX_VERSION[v2022.2] = "5.15.36-xilinx-v2022.2%"
PREFERRED_VERSION_linux-xlnx ?= "${@d.getVarFlag('XILINX_LINUX_VERSION', d.getVar('XILINX_RELEASE_VERSION')) or 'undefined'}"
